# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Kovivchak Evgen <oneonfire@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - upstream patch for AArch64

pkgname=jemalloc
epoch=1
pkgver=5.0.1
pkgrel=3.1
pkgdesc='General-purpose scalable concurrent malloc implementation'
arch=('i686' 'x86_64')
license=('BSD')
url='http://www.canonware.com/jemalloc/'
depends=('glibc')
provides=('libjemalloc.so')
optdepends=('perl: for jeprof')
source=("https://github.com/jemalloc/jemalloc/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.bz2"
        'https://github.com/jemalloc/jemalloc/commit/8cfc9dec37b312a2686f602bbcdd102ca07cca99.patch')
sha256sums=('4814781d395b0ef093b21a08e8e6e0bd3dab8762f9935bbfb71679b0dea7c3e9'
            '2a339806cb6366e31d26f4fbaed0d0c4e3b19a99479b15e74fd3580c70946774')

prepare() {
  cd $pkgname-$pkgver

  patch -p1 -i ../8cfc9dec37b312a2686f602bbcdd102ca07cca99.patch
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
