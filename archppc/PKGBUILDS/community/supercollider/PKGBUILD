# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: osc <farid at archlinux-br.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - added recommended cmake defines (last two lines)

_name=SuperCollider
pkgname=supercollider
pkgver=3.8.0
pkgrel=4
pkgdesc="An environment and programming language for real time audio synthesis and algorithmic composition"
arch=('x86_64')
url="https://supercollider.github.io"
license=('GPL3')
depends=('boost-libs' 'fftw' 'jack' 'qt5-webkit')
makedepends=('boost' 'cmake' 'emacs' 'qt5-tools')
optdepends=('emacs: emacs interface'
'gedit: gedit interface')
source=("https://github.com/${pkgname}/${pkgname}/releases/download/Version-${pkgver}/${_name}-${pkgver}-Source-linux.tar.bz2"
        "gcc7-bind-functional.patch")
install="${pkgname}.install"
sha512sums=('305898067cb323682ca3dc65b3f093658192dbad228632d928f2fbd4a635c651f49e2e4f32c338997aafd48a22b739cd80de4296f747c3f1810ddede0aff283a'
            '7b702d2bfdd18d8201d7ff5e5d2680f1137078c48e59ac15abf96d601b9eaabf8c612527a61d7987661edb73e483f91711cc80de3091b3027a21fd19cb6f8029')

prepare() {
  cd "${_name}-Source"
  # fix the use of std::bind for gcc7
  # https://github.com/supercollider/supercollider/pull/3015
  # https://github.com/supercollider/supercollider/pull/3029
  patch -Np0 -i "../gcc7-bind-functional.patch"
  # requires out-of-tree build
  [ -d bld ] || mkdir bld && cd bld
}

build() {
  cd "${_name}-Source/bld"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=Release \
           -DLIBSCSYNTH=ON \
           -DSC_VIM=OFF \
           -DSYSTEM_BOOST=ON \
           -DSSE=OFF -DSSE2=OFF -DSUPERNOVA=OFF -DNOVA_SIMD=ON -DNATIVE=OFF \
           -DCMAKE_C_FLAGS="${CFLAGS}"
  make
}

package() {
  cd "${_name}-Source/bld"
  make DESTDIR="${pkgdir}" install
  cd ..
  install -Dm644 AUTHORS ${pkgdir}/usr/share/doc/${pkgname}/AUTHORS
  install -Dm644 CHANGELOG.md ${pkgdir}/usr/share/doc/${pkgname}/CHANGELOG.md
  install -Dm644 README.md ${pkgdir}/usr/share/doc/${pkgname}/README.md
  install -Dm644 README_LINUX.md ${pkgdir}/usr/share/doc/${pkgname}/README_LINUX.md
}
