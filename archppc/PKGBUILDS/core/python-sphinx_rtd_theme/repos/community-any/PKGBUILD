# $Id: PKGBUILD 217213 2017-03-19 00:24:29Z demize $
# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: eolianoe <eolianoe [at] gmail [DoT] com>
# Contributor: Romain Schmitz <slopjong .at. slopjong .dot. de>

pkgbase=python-sphinx_rtd_theme
pkgname=('python-sphinx_rtd_theme' 'python2-sphinx_rtd_theme')
pkgver=0.2.4
pkgrel=1

pkgdesc="Python Sphinx Read The Docs Theme"
url='https://github.com/rtfd/sphinx_rtd_theme/'
arch=('any')
license=('MIT')

makedepends=('python-setuptools' 'python2-setuptools')

source=("https://pypi.org/packages/source/s/sphinx_rtd_theme/sphinx_rtd_theme-$pkgver.tar.gz")

sha256sums=('2df74b8ff6fae6965c527e97cca6c6c944886aae474b490e17f92adfbe843417')

build() {
  cp -r sphinx_rtd_theme-$pkgver sphinx_rtd_theme-$pkgver-py2

  cd sphinx_rtd_theme-$pkgver
  python setup.py build

  cd "$srcdir"/sphinx_rtd_theme-$pkgver-py2
  python2 setup.py build
}

package_python-sphinx_rtd_theme() {
  cd sphinx_rtd_theme-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-sphinx_rtd_theme() {
  cd sphinx_rtd_theme-${pkgver}-py2 
  python2 setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

