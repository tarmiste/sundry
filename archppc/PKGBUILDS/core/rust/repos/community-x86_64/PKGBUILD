# $Id: PKGBUILD 273384 2017-12-09 12:17:55Z demize $
# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Contributor: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: userwithuid <userwithuid@gmail.com>

pkgname=('rust' 'rust-docs')
epoch=1
pkgver=1.22.1
pkgrel=1

pkgdesc='Systems programming language focused on safety, speed and concurrency'
url='https://www.rust-lang.org/'
arch=('x86_64')
license=('MIT' 'Apache')

makedepends=('rust' 'libffi' 'perl' 'python2' 'curl' 'cmake')

options=('!emptydirs')

source=("https://static.rust-lang.org/dist/rustc-$pkgver-src.tar.gz"{,.asc}
        bootstrap-config.toml)

sha256sums=('8b7a42bdd6eb205a8c533eb41b5c42389a88158d060aed1e0f461f68c1fd3fd3'
            'SKIP'
            '6c25c451d6286efd19e96a552406c8b299345749577cf924699cf4c3831d9ad0')
validpgpkeys=('108F66205EAEB0AAA8DD5E1C85AB96E6FA1BE5FE') # Rust Language (Tag and Release Signing Key) <rust-key@rust-lang.org>

prepare() {
  cd "rustc-$pkgver-src"

  cp "$srcdir"/bootstrap-config.toml config.toml
}

build() {
  cd "rustc-$pkgver-src"

  msg2 "Run build under x.py"
  python2 ./x.py build
}

package_rust() {
  depends=('gcc-libs' 'llvm-libs')
  provides=('cargo')
  conflicts=('cargo')
  replaces=('cargo')


  cd "rustc-$pkgver-src"

  DESTDIR="$pkgdir" python2 ./x.py install

  for license in APACHE MIT; do install -Dm644 "LICENSE-$license" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-$license"; done

  cd "$pkgdir/usr/lib"

  rm rustlib/{components,manifest-rustc,rust-installer-version}
  ln -sf rustlib/$CARCH-unknown-linux-gnu/lib/*.so .

  # move docs out of the way
  mv "$pkgdir/usr/share/doc" "$srcdir/"
}

package_rust-docs() {
  msg2 "Packaging documentation"
  install -d "$pkgdir/usr/share/doc/"
  mv "$srcdir"/doc/* "$pkgdir"/usr/share/doc/rust/

  msg2 "Packaging license files for the documentation"
  for license in APACHE MIT; do install -Dm644 "rustc-$pkgver-src/LICENSE-$license" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-$license"; done
}

# vim:set ts=2 sw=2 et:
