# $Id: PKGBUILD 268326 2017-11-24 13:12:09Z archange $
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Frank Ickstadt (frank dot ickstadt at gmail dot com)

pkgname=argyllcms
_pkgname=Argyll
pkgver=2.0.0
_pkgver="V${pkgver}"
pkgrel=1
pkgdesc="An ICC compatible color management system with support for different colorimeter hardware"
arch=('x86_64')
url="https://www.argyllcms.com/"
license=('GPL' 'AGPL')
depends=('libpng' 'libtiff' 'libxss' 'libxinerama' 'libxxf86vm' 'libxrandr' 'zlib' 'openssl')
makedepends=('ftjam' 'zip' 'unzip')
source=("https://www.argyllcms.com/${_pkgname}_${_pkgver}_src.zip"
        "${pkgname}.sysusers")
install=${pkgname}.install
sha256sums=('5492896c040b406892864c467466ad6b50eb62954b5874ef0eb9174d1764ff41'
            '20c6482069c140c19f5d804ded9a85fbc746160aee9e04da712b8f6b30939a1b')

prepare() {
  cd ${_pkgname}_${_pkgver}
  # Use hwdb builtin, instead of the obsolete usb-db in the udev rules.
  sed -i 's:^TEST=="/lib/udev/usb-db", IMPORT{program}="usb-db %p":IMPORT{builtin}="hwdb --subsystem=usb":' usb/55-Argyll.rules

  # Fix deprecated SSL method
  #sed -i 's:TLSv1_client_method():SSLv23_client_method:' ccast/ccpacket.c

  # Use LDFLAGS
  echo "LINKFLAGS += ${LDFLAGS} ;" >> Jamtop
}

build() {
  cd ${_pkgname}_${_pkgver}
  CCOPTFLAG="$CFLAGS"
  jam -q -fJambase ${MAKEFLAGS} -sPREFIX=/usr -sDESTDIR="${pkgdir}" -sREFSUBDIR=share/argyllcms/ref all
}

package() {
  cd ${_pkgname}_${_pkgver}
  jam -q -fJambase ${MAKEFLAGS} -sPREFIX=/usr -sDESTDIR="${pkgdir}" -sREFSUBDIR=share/argyllcms/ref install

  rm "${pkgdir}"/usr/bin/License.txt

  rm {doc,ref}/afiles
  install -Dm644 doc/*.* -t "${pkgdir}"/usr/share/${pkgname}/doc/
  install -Dm644 doc/ccmxs/*.ccmx -t "${pkgdir}"/usr/share/${pkgname}/doc/ccmxs/

  install -Dm644 usb/55-Argyll.rules -t "${pkgdir}"/usr/lib/udev/rules.d/

  install -Dm644 "${srcdir}"/${pkgname}.sysusers "${pkgdir}"/usr/lib/sysusers.d/${pkgname}.conf
}

# vim:set ts=2 sw=2 et:
