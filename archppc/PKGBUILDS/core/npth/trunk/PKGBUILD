# $Id: PKGBUILD 310007 2017-11-15 14:11:34Z foutrelis $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=npth
pkgver=1.5
pkgrel=1
pkgdesc='New portable threads library'
url='http://git.gnupg.org/cgi-bin/gitweb.cgi?p=npth.git'
license=('LGPL')
options=('!libtool')
arch=('x86_64')
validpgpkeys=('D8692123C4065DEA5E0F3AB5249B39D24F25E3B6'
              '46CC730865BB5C78EBABADCF04376F3EE0856959'
              '031EC2536E580D8EA286A9F22071B08A33BD3F06'
              'D238EA65D64C67ED4C3073F28A861B1C7EFD60D9')
source=("ftp://ftp.gnupg.org/gcrypt/${pkgname}/${pkgname}-${pkgver}.tar.bz2"{,.sig})
sha256sums=('294a690c1f537b92ed829d867bee537e46be93fbd60b16c04630fbbfcd9db3c2'
            'SKIP')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr --enable-maintainer-mode
	make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make check
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
