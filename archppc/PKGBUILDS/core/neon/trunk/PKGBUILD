# $Id: PKGBUILD 310007 2017-11-15 14:11:34Z foutrelis $
# Maintainer:
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=neon
pkgver=0.30.2
pkgrel=2
pkgdesc="HTTP and WebDAV client library with a C interface"
arch=('x86_64')
url="http://www.webdav.org/neon/"
license=('GPL' 'LGPL')
depends=('krb5' 'expat' 'ca-certificates' 'zlib')
options=('libtool') # FS#16067
source=(http://www.webdav.org/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.asc})
sha1sums=('d1c020f96731135263476ebaa72b2da07c4727cd'
          'SKIP')
validpgpkeys=('190555472DCC589BEF01609C608A86DF9833CC49') # Joe Orton

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --with-expat \
    --enable-shared \
    --disable-static \
    --with-ssl=openssl \
    --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
