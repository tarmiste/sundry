# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Contributor: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG
# Contributor: 謝致邦 <Yeking@Red54.com>
# Contributor: cantabile <cantabile dot desu at gmail dot com>
# Contributor: reMiND <detanator@gmail.com>

pkgname=rhash
pkgver=1.3.5
pkgrel=2

pkgdesc='Utility for verifying hash sums of files'
url='http://rhash.anz.ru/?l=en'
license=('custom' 'MIT')
arch=('i686' 'x86_64')

depends=('glibc')

backup=('etc/rhashrc')

source=("https://downloads.sourceforge.net/rhash/rhash-$pkgver-src.tar.gz")

sha256sums=('98e0688acae29e68c298ffbcdbb0f838864105f9b2bd8857980664435b1f1f2e')

build() {
  cd RHash-$pkgver
  export ADDCFLAGS="$CFLAGS"
  export ADDLDFLAGS="$LDFLAGS"
  make build-shared
  make -C librhash
}

check() {
  cd RHash-$pkgver
  make test-shared
}

package() {
  cd RHash-$pkgver

  # program
  make PREFIX=/usr DESTDIR="$pkgdir" install-shared

  # library
  make -C librhash PREFIX=/usr DESTDIR="$pkgdir" install-headers install-lib-shared install-so-link

  # license
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/rhash/LICENSE
}
