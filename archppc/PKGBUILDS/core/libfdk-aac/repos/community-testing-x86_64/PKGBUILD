# $Id: PKGBUILD 195290 2016-11-10 14:08:58Z alucryd $
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: PelPix <kylebloss@pelpix.info>
# Contributor: DrZaius <lou[at]fakeoutdoorsman[dot]com>

pkgname=libfdk-aac
pkgver=0.1.4.r88.5fd7e65
pkgrel=1
pkgdesc='Fraunhofer FDK AAC codec library'
arch=('i686' 'x86_64')
url='http://sourceforge.net/projects/opencore-amr/'
license=('custom')
depends=('glibc')
makedepends=('git')
provides=('libfdk-aac.so')
_commit='5fd7e6537d1f37fb1bec8c6aa2f8e22030ab10de'
source=("git+https://github.com/mstorsjo/fdk-aac.git#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd fdk-aac

  git describe | sed 's/^v//; s/-/.r/; s/-g/./'
}

build() {
  cd fdk-aac

  ./autogen.sh
  ./configure \
    --prefix='/usr' \
    --disable-static
  make
}

package () {
  cd fdk-aac

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/share/licenses/libfdk-aac
  install -m 644 NOTICE "${pkgdir}"/usr/share/licenses/libfdk-aac/
}

# vim: ts=2 sw=2 et:
