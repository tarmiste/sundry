# $Id: PKGBUILD 266875 2017-11-15 14:29:11Z foutrelis $
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>

pkgname=libreplaygain
pkgver=475
pkgrel=2
pkgdesc="A library to adjust audio gain"
arch=('x86_64')
url="http://www.musepack.net/index.php?pg=src"
license=('LGPL')
depends=('glibc')
makedepends=('cmake')
source=("http://files.musepack.net/source/${pkgname}_r${pkgver}.tar.gz")
md5sums=('e27b3b1249b7fbae92d656d9e3d26633')

build() {
  cd "${srcdir}"

  mkdir build
  cd build
  cmake ../${pkgname}_r${pkgver}/ \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${srcdir}"/build

  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}"/usr/include/replaygain/
  install -Dm644 "${srcdir}"/${pkgname}_r${pkgver}/include/replaygain/* \
    "${pkgdir}"/usr/include/replaygain/
}
