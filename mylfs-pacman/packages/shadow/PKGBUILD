pkgname=shadow
pkgver=4.5
pkgrel=1
pkgdesc="Password and account management tool suite with support for shadow files and PAM"
arch=('x86_64')
url="https://github.com/shadow-maint/shadow"
license=('BSD')
source=(${pkgname}-${pkgver}.tar.xz)
install=shadow.install

prepare() {
    cd "${pkgname}-${pkgver}"

    sed -i 's/groups$(EXEEXT) //' src/Makefile.in
    find man -name Makefile.in -exec sed -i 's/groups\.1 / /'   {} \;
    find man -name Makefile.in -exec sed -i 's/getspnam\.3 / /' {} \;
    find man -name Makefile.in -exec sed -i 's/passwd\.5 / /'   {} \;

    sed -i -e 's@#ENCRYPT_METHOD DES@ENCRYPT_METHOD SHA512@' \
	-e 's@/var/spool/mail@/var/mail@' etc/login.defs

    sed -i 's/1000/999/' etc/useradd
}

build() {
    cd "${pkgname}-${pkgver}"
    ./configure --sysconfdir=/etc --with-group-name-max-length=32
    make
}

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR=${pkgdir} install

    install -vdm755 ${pkgdir}/bin
    mv -v ${pkgdir}/usr/bin/passwd ${pkgdir}/bin
}
