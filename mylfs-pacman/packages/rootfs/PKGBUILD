pkgname=rootfs
pkgver=1.0
pkgrel=1
pkgdesc="Skeleton directory layout and core /etc and /var/log files for initial install"
arch=('any')
license=('Creative Commons Attribution-NonCommercial-ShareAlike 4.0')
groups=('core')
source=('passwd'
        'group')
backup=('etc/passwd'
        'etc/group'
        'var/log/btmp'
        'var/log/lastlog'
        'var/log/wtmp')

package(){
  #6.2
  install -vdm755 ${pkgdir}/{dev,proc,run,sys}
  mknod -m 666 ${pkgdir}/dev/console c 5 1
  mknod -m 666 ${pkgdir}/dev/null c 1 3
  chmod -v 1777 ${pkgdir}/run
  chmod -v 555 ${pkgdir}/{proc,sys}

  #6.5
  install -vdm755 ${pkgdir}/{bin,boot,etc/{opt,sysconfig},home,lib/firmware,mnt,opt}
  install -vdm755 ${pkgdir}/{media/{floppy,cdrom},sbin,srv,var}
  install -dv -m 0750 ${pkgdir}/root
  install -dv -m 1777 ${pkgdir}/tmp ${pkgdir}/var/tmp
  install -vdm755 ${pkgdir}/usr/{,local/}{bin,include,lib,sbin,src}
  install -vdm755 ${pkgdir}/usr/{,local/}share/{color,dict,doc,info,locale,man}
  install -vdm755 ${pkgdir}/usr/{,local/}share/{misc,terminfo,zoneinfo}
  install -vdm755 ${pkgdir}/usr/libexec
  install -vdm755 ${pkgdir}/usr/{,local/}share/man/man{1..8}
  
  case $(uname -m) in
   *64) install -vdm755 ${pkgdir}/lib64 ;;
  esac
  
  install -vdm755 ${pkgdir}/var/{log,mail,spool}
  ln -sv ../run ${pkgdir}/var/run
  ln -sv ../run/lock ${pkgdir}/var/lock
  install -vdm755 ${pkgdir}/var/{opt,cache,lib/{color,misc,locate},local}
  
  #6.6
  ln -sv /proc/self/mounts ${pkgdir}/etc/mtab
  install -vm644 ${srcdir}/passwd ${pkgdir}/etc/passwd
  install -vm644 ${srcdir}/group ${pkgdir}/etc/group
  touch ${pkgdir}/var/log/{btmp,lastlog,faillog,wtmp}
  chgrp -v utmp ${pkgdir}/var/log/lastlog
  chmod -v 664  ${pkgdir}/var/log/lastlog
  chmod -v 600  ${pkgdir}/var/log/btmp
  ln -sv bash ${pkgdir}/bin/sh

}
sha256sums=('8cfa7e89d3f2b6298d2892ce59e5617b916d6df672135d72a8ed029fa88408e1'
            '1690f4982e5836aef1858ee84885c723bc056a040416bdb5f52196807de3fcad')
