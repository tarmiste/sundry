pkgname=acl
pkgver=2.2.52
pkgrel=1
pkgdesc="Access control list utilities, libraries and headers"
arch=('x86_64')
url="http://savannah.nongnu.org/projects/acl"
license=('LGPL')
source=(${pkgname}-${pkgver}.src.tar.gz)

prepare() {
    cd "${pkgname}-${pkgver}"
    sed -i -e 's|/@pkg_name@|&-@pkg_version@|' include/builddefs.in
    sed -i "s:| sed.*::g" test/{sbits-restore,cp,misc}.test
    sed -i -e "/TABS-1;/a if (x > (TABS-1)) x = (TABS-1);" \
	libacl/__acl_to_any_text.c
}

build() {
    cd "${pkgname}-${pkgver}"
    ./configure --prefix=/usr    \
		--bindir=/bin    \
		--disable-static \
		--libexecdir=/usr/lib
    make
}

package() {
    cd "${pkgname}-${pkgver}"
    
    make DIST_ROOT="$pkgdir" install install-dev install-lib

    chmod -v 755 ${pkgdir}/usr/lib/libacl.so
    install -vdm755 ${pkgdir}/lib
    mv -v ${pkgdir}/usr/lib/libacl.so.* ${pkgdir}/lib
    ln -sfv ../../lib/$(readlink ${pkgdir}/usr/lib/libacl.so) ${pkgdir}/usr/lib/libacl.so
}
