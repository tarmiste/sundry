pkgname=bc
pkgver=1.07.1
pkgrel=1
pkgdesc="An arbitrary precision calculator language"
arch=('x86_64')
url="https://www.gnu.org/software/bc/"
license=('GPL')
source=(${pkgname}-${pkgver}.tar.gz)

prepare() {
    cd "${pkgname}-${pkgver}"
    cat > bc/fix-libmath_h << "EOF"
#! /bin/bash
sed -e '1   s/^/{"/' \
    -e     's/$/",/' \
    -e '2,$ s/^/"/'  \
    -e   '$ d'       \
    -i libmath.h

sed -e '$ s/$/0}/' \
    -i libmath.h
EOF

    sed -i -e '/flex/s/as_fn_error/: ;; # &/' configure
}

build() {
    cd "${pkgname}-${pkgver}"
    ./configure --prefix=/usr           \
		--with-readline         \
		--mandir=/usr/share/man \
		--infodir=/usr/share/info
    make
}

check() {
    cd "${pkgname}-${pkgver}"
    echo "quit" | ./bc/bc -l Test/checklib.b 2>&1 | tee ${pkgname}-${pkgver}-check-log
}

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR=${pkgdir} install
}
