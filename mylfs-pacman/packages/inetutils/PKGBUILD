pkgname=inetutils
pkgver=1.9.4
pkgrel=1
pkgdesc="A collection of common network programs"
arch=('x86_64')
url="http://www.gnu.org/software/inetutils/"
license=('GPL3')
source=(${pkgname}-${pkgver}.tar.xz)

build() {
    cd "${pkgname}-${pkgver}"
    ./configure --prefix=/usr        \
		--localstatedir=/var \
		--disable-logger     \
		--disable-whois      \
		--disable-rcp        \
		--disable-rexec      \
		--disable-rlogin     \
		--disable-rsh        \
		--disable-servers
    make
}

check() {
    cd "${pkgname}-${pkgver}"
    make check 2>&1 | tee ${pkgname}-${pkgver}-check-log
}

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR=${pkgdir} install

    install -vdm755 ${pkgdir}/bin
    install -vdm755 ${pkgdir}/sbin
    mv -v ${pkgdir}/usr/bin/{hostname,ping,ping6,traceroute} ${pkgdir}/bin
    mv -v ${pkgdir}/usr/bin/ifconfig ${pkgdir}/sbin
}
