# $Id: PKGBUILD 310007 2017-11-15 14:11:34Z foutrelis $
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=libtasn1
pkgver=4.12
pkgrel=2
pkgdesc="The ASN.1 library used in GNUTLS"
arch=('x86_64')
license=('GPL3' 'LGPL')
url="https://www.gnu.org/software/libtasn1/"
depends=('glibc')
source=(https://ftp.gnu.org/gnu/libtasn1/${pkgname}-${pkgver}.tar.gz{,.sig})
sha256sums=('6753da2e621257f33f5b051cc114d417e5206a0818fe0b1ecfd6153f70934753'
            'SKIP')
validpgpkeys=('1F42418905D8206AA754CCDC29EE58B996865171') #Nikos Mavrogiannopoulos <nmav@gnutls.org>

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
      --with-packager=Archlinux \
      --with-packager-bug-reports="http://bugs.archlinux.org/" \
      --with-packager-version=${pkgver}-${pkgrel}
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  make -C doc/reference DESTDIR="${pkgdir}" install-data-local
}
