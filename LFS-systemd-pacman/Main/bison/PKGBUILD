# Maintainer: DJ Lucas <dj_AT_linuxfromscratch_DOT_org>
pkgname=bison
pkgver=3.0.4
pkgrel=1
pkgdesc="The Bison package contains a parser generator."
arch=('x86_64')
url="http://www.gnu.org/software/${pkgname}/"
groups=('core')
license=('GPL3')
makedepends=('bash'
             'binutils'
             'coreutils'
             'gcc'
             'glibc'
             'grep'
             'm4'
             'make'
             'perl'
             'sed')
depends=('bash'
         'glibc'
         'm4')
checkdepends=('diffutils'
              'findutils'
              'flex')
source=("http://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz")

build(){
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr \
              --docdir=/usr/share/doc/${pkgname}-${pkgver}
  make
}

check(){
  # needs flex, unable to run check if bootstrapping
  cd ${srcdir}/${pkgname}-${pkgver}
  make check || true
  # we expect 3 failures
  failures=`grep -A1 "^ERROR" tests/testsuite.log | \
            grep failed | cut -d " " -f 1`
  test x"$failures" != "x" || failures="0"
  if test "$failures" -gt "3"; then
      echo "Failures exceeded 3." && exit 1
  fi
}

package(){
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
sha256sums=('a72428c7917bdf9fa93cb8181c971b6e22834125848cf1d03ce10b1bb0716fe1')
