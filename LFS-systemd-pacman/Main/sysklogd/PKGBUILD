# Maintainer: DJ Lucas <dj_AT_linuxfromscratch_DOT_org>

pkgname=sysklogd
pkgver=1.5.1
pkgrel=1
pkgdesc="The Sysklogd package contains programs for logging system messages, such as those given by the kernel when unusual things happen. "
arch=('x86_64')
url="http://www.infodrom.org/projects/sysklogd"
groups=('core')
license=('GPL')
makedepends=('binutils'
             'coreutils'
             'gcc'
             'glibc'
             'make'
             'patch')
depends=('glibc')
source=("http://www.infodrom.org/projects/${pkgname}/download/${pkgname}-${pkgver}.tar.gz")

build(){
  cd "${pkgname}-${pkgver}"
  sed -i '/Error loading kernel symbols/{n;n;d}' ksym_mod.c
  sed -i 's/union wait/int/' syslogd.c
  make
}

package(){
  cd "${pkgname}-${pkgver}"
  mkdir -pv ${pkgdir}/usr/share/man/man5
  mkdir -pv ${pkgdir}/usr/share/man/man8
  mkdir -pv ${pkgdir}/sbin
  make prefix="${pkgdir}" BINDIR="${pkgdir}"/sbin install
  mkdir ${pkgdir}/etc
cat > ${pkgdir}/etc/syslog.conf << "EOF"
# Begin /etc/syslog.conf

auth,authpriv.* -/var/log/auth.log
*.*;auth,authpriv.none -/var/log/sys.log
daemon.* -/var/log/daemon.log
kern.* -/var/log/kern.log
mail.* -/var/log/mail.log
user.* -/var/log/user.log
*.emerg *

# End /etc/syslog.conf
EOF
}

sha256sums=('5166c185ae23c92e8b9feee66a6e3d0bc944bf673112f53e3ecf62e08ce7c201')
