# $Id: PKGBUILD 310833 2017-11-24 22:34:20Z jelle $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=kbd
pkgver=2.0.4
pkgrel=1
pkgdesc="Keytable files and keyboard utilities"
arch=('x86_64')
url="http://www.kbd-project.org"
license=('GPL')
depends=('glibc' 'pam')
makedepends=('check')
source=(https://www.kernel.org/pub/linux/utils/${pkgname}/${pkgname}-${pkgver}.tar.gz
https://www.kernel.org/pub/linux/utils/kbd/${pkgname}-${pkgver}.tar.sign
        'kbd-2.0.4-backspace-1.patch')
provides=('vlock')
conflicts=('vlock')
replaces=('vlock')
validpgpkeys=(
              '7F2A3D07298149A0793C9A4EA45ABA544CFFD434' #Alexey Gladkov 
             )

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i ../kbd-2.0.4-backspace-1.patch
  sed -i 's/\(RESIZECONS_PROGS=\)yes/\1no/g' configure
  sed -i 's/resizecons.8 //' docs/man/man8/Makefile.in
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  if [ -d /tools ]
  then
    PKG_CONFIG_PATH=/tools/lib/pkgconfig ./configure --prefix=/usr --disable-vlock
  else
    ./configure --prefix=/usr --disable-vlock
  fi
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
md5sums=('c69fce737a5fd0fa686cbfc4f611a220'
         'SKIP'
         'f75cca16a38da6caa7d52151f7136895')
