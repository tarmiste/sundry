# $Id: PKGBUILD 309927 2017-11-14 18:30:42Z andyrtr $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libpipeline
pkgver=1.4.2
pkgrel=1
pkgdesc="a C library for manipulating pipelines of subprocesses in a flexible and convenient way"
arch=('x86_64')
url="https://libpipeline.nongnu.org/"
license=('GPL')
depends=('glibc')
source=(https://download.savannah.gnu.org/releases/libpipeline/$pkgname-$pkgver.tar.gz{,.sig})
sha256sums=('fef1fc9aa40ce8796f18cd1aecd888a9484a9556c8b0f8d07c863578277679be'
            'SKIP')
validpgpkeys=('AC0A4FF12611B6FCCF01C111393587D97D86500B') # Colin Watson <cjwatson@debian.org>

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
}
