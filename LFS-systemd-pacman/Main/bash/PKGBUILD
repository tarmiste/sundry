# Maintainer: DJ Lucas <dj_AT_linuxfromscratch_DOT_org>

pkgname=bash
pkgver=4.4
pkgrel=1
pkgdesc="The Bash package contains the Bourne-Again SHell."
arch=('x86_64')
url="http://www.gnu.org/software/${pkgname}/"
groups=('core')
license=('GPL')
makedepends=('bash'
             'binutils'
             'bison'
             'coreutils'
             'gcc'
             'grep'
             'make'
             'patch'
             'sed'
             'texinfo')
depends=('glibc'
         'ncurses'
         'readline'
         'gettext')
_confargs=""
source=("http://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "http://www.linuxfromscratch.org/patches/downloads/${pkgname}/${pkgname}-${pkgver}-upstream_fixes-1.patch")

build(){
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/${pkgname}-${pkgver}-upstream_fixes-1.patch"
  ./configure --prefix=/usr                                \
              --docdir=/usr/share/doc/${pkgname}-${pkgver} \
              --without-bash-malloc                        \
              --with-installed-readline
  make
}

check(){
  cd "${pkgname}-${pkgver}"
  make tests
}

package(){
  # Setup for LFS /usr merge filesystem layout
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -vdm755 "${pkgdir}/bin"
  mv -vf "${pkgdir}/usr/bin/bash" "${pkgdir}/bin"
}

sha256sums=('d86b3392c1202e8ff5a423b302e6284db7f8f435ea9f39b5b1b20fd3ac36dfcb'
            '8f6f68a25a4f16ffeae3f24a0c3e69af2dbcff6f2c4fb805cd4e18fd96f06016')
