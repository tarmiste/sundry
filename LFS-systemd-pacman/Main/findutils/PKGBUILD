# $Id: PKGBUILD 310007 2017-11-15 14:11:34Z foutrelis $
# Maintainer:

pkgname=findutils
pkgver=4.6.0
pkgrel=1
pkgdesc="GNU utilities to locate files"
arch=('x86_64')
license=('GPL3')
groups=('base' 'base-devel')
depends=('glibc' 'sh')
url="http://www.gnu.org/software/findutils"
source=(ftp://ftp.gnu.org/pub/gnu/findutils/${pkgname}-${pkgver}.tar.gz)
sha1sums=('f18e8aaee3f3d4173a1f598001003be8706d28b0')
#validpgpkeys=('A15B725964A95EE5') # James Youngman <james@youngman.org>
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Suppress a test which on some machines can loop forever:
  sed -i 's/test-lock..EXEEXT.//' tests/Makefile.in

  ./configure --prefix=/usr --localstatedir=/var/lib/locate
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR=$pkgdir install
}
