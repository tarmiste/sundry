# Maintainer: DJ Lucas <dj_AT_linuxfromscratch_DOT_org>

pkgname=libcap
pkgver=2.25
pkgrel=1
pkgdesc="The Libcap package implements the user-space interfaces to the POSIX 1003.1e capabilities available in Linux kernels. These capabilities are a partitioning of the all powerful root privilege into a set of distinct privileges."
arch=('x86_64')
url="https://sites.google.com/site/fullycapable/"
license=('GPL3')
groups=('core')
source=("https://www.kernel.org/pub/linux/libs/security/linux-privs/libcap2/${pkgname}-${pkgver}.tar.xz")
depends=('glibc' 'linux-pam')
makedepends=('attr'
             'bash'
             'binutils'
             'coreutils'
             'gcc'
             'make'
             'perl'
             'sed')
backup=('etc/security/capability.conf')

build() {
  cd "${pkgname}-${pkgver}"
  sed -i '/install.*STALIBNAME/d' libcap/Makefile
  sed -e '/GPERF_OUTPUT/s/-include/-include stddef.h -include/' \
      -e '/__cap_lookup_name/s/unsigned int/register size_t/' \
      -i.bak libcap/Makefile
  make
  make -C pam_cap
}

package() {
  cd "${pkgname}-${pkgver}"
  install -vdm755 "${pkgdir}/usr/lib" "${pkgdir}"/{etc,lib}/security
  make RAISE_SETFCAP=no lib=lib prefix="${pkgdir}/usr" install
  chmod -v 755 "${pkgdir}/usr/lib/libcap.so"
  install -v -m755 pam_cap/pam_cap.so "${pkgdir}/lib/security"
  install -v -m644 pam_cap/capability.conf "${pkgdir}/etc/security"
}

sha256sums=('693c8ac51e983ee678205571ef272439d83afe62dd8e424ea14ad9790bc35162')
