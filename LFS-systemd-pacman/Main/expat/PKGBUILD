# Maintainer:  DJ Lucas <dj_AT_linuxfromscratch_DOT_org>

pkgname=expat
pkgver=2.2.4
pkgrel=1
pkgdesc="The Expat package contains a stream oriented C library for parsing XML."
arch=('x86_64')
url="http://expat.sourceforge.net/"
license=('custom')
groups=('core')
depends=('glibc')
makedepends=('bash'
             'binutils'
             'coreutils'
             'gawk'
             'gcc'
             'grep'
             'make'
             'sed')
source=("http://prdownloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2")

build() {
  cd "${pkgname}-${pkgver}"
  sed -i 's|usr/bin/env |bin/|' run.sh.in
  ./configure --prefix=/usr \
              --disable-static
  make
}

check() {
  cd "${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -vdm755 "${pkgdir}/usr/share/doc/${pkgname}-${pkgver}"
  install  -vm644 doc/*.{html,png,css} \
                  "${pkgdir}/usr/share/doc/${pkgname}-${pkgver}"
}

sha256sums=('03ad85db965f8ab2d27328abcf0bc5571af6ec0a414874b2066ee3fdd372019e')

